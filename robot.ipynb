{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Distributions, Plots, Random, LinearAlgebra, StatsBase, LaTeXStrings"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function sample_robot_kinematics(x, u, Δt, ϵ)\n",
    "    # Extract the pose and velocity from the state and control vector\n",
    "    X, Y, θ, v, ω = x[1], x[2], x[3], u[1], u[2]\n",
    "    \n",
    "    # Compute the variance in the velocities\n",
    "    σ²_v = ϵ[1] * v^2 + ϵ[2] * ω^2\n",
    "    σ²_ω = ϵ[3] * v^2 + ϵ[4] * ω^2\n",
    "    \n",
    "    # Compute the variance in the final perturbation to the orientation\n",
    "    σ²_γ = ϵ[5] * v^2 + ϵ[6] * ω^2\n",
    "    \n",
    "    # Compute the velocities with the sampled errors\n",
    "    v̂ = v + rand(Normal(0., σ²_v))\n",
    "    ω̂ = ω + rand(Normal(0., σ²_ω))\n",
    "    \n",
    "    # Compute the perturbation to the final orientation\n",
    "    γ̂ = rand(Normal(0., σ²_γ))\n",
    "    \n",
    "    # Compute the final pose\n",
    "    X̂ = X - (v̂ / ω̂) * sin(θ) + (v̂ / ω̂) * sin(θ + ω̂ * Δt)\n",
    "    Ŷ = Y + (v̂ / ω̂) * cos(θ) - (v̂ / ω̂) * cos(θ + ω̂ * Δt)\n",
    "    θ̂ = θ + ω̂ * Δt + γ̂ * Δt\n",
    "        \n",
    "    # Return the final pose\n",
    "    return [X̂, Ŷ, θ̂]\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function plot_robot(x, r, color)\n",
    "    # Angles to plot the robot as a circle\n",
    "    ϕs = 0:0.001:2π\n",
    "    \n",
    "    # Plot the robot shape\n",
    "    plot!(x[1] .+ r*sin.(ϕs), x[2] .+ r*cos.(ϕs), seriestype=[:shape,], label=\"\", alpha=0.25, color=color)\n",
    "    \n",
    "    # Plot a small line on the robot to show its orientation\n",
    "    plot!([x[1], x[1] + r * cos(x[3])], [x[2], x[2] + r * sin(x[3])], color=\"black\", label=\"\", alpha=0.25)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function plot_map(map)\n",
    "    # Plot each object in the map\n",
    "    for obj in map\n",
    "        # Plot the object\n",
    "        plot!(obj[:,1], obj[:,2], label=\"\", color=\"black\", seriestype=[:shape,], alpha=0.25)\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function create_box_obj(loc, angle)\n",
    "    # Create a unit box\n",
    "    box = [[0., 0.] [1., 0.] [1., 1.] [0., 1.] [0., 0.]]\n",
    "    \n",
    "    # Construct the rotation matrix for the angle\n",
    "    R = [[cos(angle), -sin(angle)] [sin(angle), cos(angle)]]'\n",
    "    \n",
    "    # Apply the rotation to the unit box\n",
    "    box = (R * box)'\n",
    "    \n",
    "    # Move the box to the final location\n",
    "    box[:,1] = box[:,1] .+ loc[1]\n",
    "    box[:,2] = box[:,2] .+ loc[2]\n",
    "    \n",
    "    # Return the constructed box\n",
    "    return box\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function ray_segment_intersection(o, d, pt1, pt2)\n",
    "    # Compute three intermediate vector quantities\n",
    "    v1 = o - pt1\n",
    "    v2 = pt2 - pt1\n",
    "    v3 = [-d[2], d[1]]\n",
    "    \n",
    "    # Compute the denominator of the determinative expression\n",
    "    denom = dot(v2, v3)\n",
    "    \n",
    "    # Check if the segment is parallel to the ray\n",
    "    if abs(denom) < 1e-7\n",
    "        return Inf\n",
    "    end\n",
    "    \n",
    "    # Compute the intersection point of the ray and the segmen\n",
    "    t1 = (v2[1]*v1[2] - v2[2]*v1[1]) / denom\n",
    "    t2 = dot(v1, v3) / denom\n",
    "    \n",
    "    # Determine if the ray intersects the segment\n",
    "    if t1 ≥ 0 && 0 ≤ t2 ≤1\n",
    "        return t1\n",
    "    else\n",
    "        return Inf\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function get_distances(x, robot)\n",
    "    # Get the orientation angle of the robot\n",
    "    θ = x[3]\n",
    "    \n",
    "    # Get the center of the robot\n",
    "    o = x[1:2]\n",
    "    \n",
    "    # Construct the angles of the sensor directions in the robot grame\n",
    "    dϕ = 2π / robot.num_sensors\n",
    "    ϕs = collect(0:dϕ:2π-1e-12)\n",
    "    \n",
    "    # Allocate a list of actual ranges\n",
    "    ranges = []\n",
    "    \n",
    "    # Compute the range along each sensor direction\n",
    "    for ϕ in ϕs\n",
    "        # Compute the sensor direction in the global frame\n",
    "        ψ = θ + ϕ\n",
    "        d = [cos(ψ), sin(ψ)]\n",
    "        \n",
    "        # Allocate a list for all the intersections with segments in the map\n",
    "        distances = []\n",
    "        \n",
    "        # Iterate over each object in the map\n",
    "        for obj in robot.map\n",
    "            # Iterate over each edge in the object\n",
    "            for i in 1:size(obj, 1) - 1\n",
    "                # Compute the intersection of the sensor ray with the edge\n",
    "                push!(distances, ray_segment_intersection(o, d, obj[i,:], obj[i+1,:]))\n",
    "            end\n",
    "        end\n",
    "        \n",
    "        # Add the closest intersection distance to the list of ranges for the sensors\n",
    "        push!(ranges, minimum(distances))\n",
    "    end\n",
    "    \n",
    "    # Return the ranges along each sensor direction\n",
    "    return ranges\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function p_hit(zk, zk_star, zk_max, σ_hit)\n",
    "    # Normal distribution modelling correct range with local noise\n",
    "    N = Normal(zk_star, σ_hit^2)\n",
    "    \n",
    "    # Compute the normalization constant\n",
    "    η = 1 / (cdf(N, zk_max) - cdf(N, 0))\n",
    "    \n",
    "    # Check if the actual range is beyond the sensor range\n",
    "    if zk_star ≥ zk_max\n",
    "        # Return a max sensor reading with probability 1\n",
    "        return zk == zk_max ? 1 : 0\n",
    "    end\n",
    "    \n",
    "    # Return the probability\n",
    "    return 0 ≤ zk ≤ zk_max ? η * pdf(N, zk) : 0\n",
    "end\n",
    "\n",
    "function p_short(zk, zk_star, λ_short)\n",
    "    # Exponential distribution to model a short measurement\n",
    "    E = Exponential(1 / λ_short)\n",
    "    \n",
    "    # Compute the normalization constant\n",
    "    η = 1 / (cdf(E, zk_star) - cdf(E, 0))\n",
    "    \n",
    "    # Return the probability\n",
    "    return 0 ≤ zk ≤ zk_star ? η * pdf(E, zk) : 0\n",
    "end\n",
    "\n",
    "function p_max(zk, zk_max)\n",
    "    # Return the probability when a sensor failure happens\n",
    "    return zk == zk_max ? 1 : 0\n",
    "end\n",
    "\n",
    "function p_rand(zk, zk_max)\n",
    "    # Return the probability when an unexplainable measurement is made\n",
    "    return 0 ≤ zk ≤ zk_max ? 1 / zk_max : 0\n",
    "end\n",
    "\n",
    "struct sensor_model\n",
    "    zk_max\n",
    "    σ_hit\n",
    "    λ_short\n",
    "    weights\n",
    "end\n",
    "\n",
    "function p_sensor(zk, zk_star, model)\n",
    "    # Compute the probability contribution from each type of measurement/failure mode\n",
    "    ps = [p_hit(zk, zk_star, model.zk_max, model.σ_hit), \n",
    "          p_short(zk, zk_star, model.λ_short),\n",
    "          p_max(zk, model.zk_max),\n",
    "          p_rand(zk, model.zk_max)]\n",
    "    \n",
    "    # Return the weighted sum of the probabilities\n",
    "    return model.weights' * ps\n",
    "end\n",
    "\n",
    "function p_sensor_array(z, x, robot)\n",
    "    # Compute what the actual ranges would be for state \"x\" using the map\n",
    "    z_star_bel = get_distances(x, robot)\n",
    "    \n",
    "    # Initialize the the probability as 1\n",
    "    p = 1\n",
    "    \n",
    "    # Update the probability with each sensor measurement\n",
    "    for k = 1:robot.num_sensors\n",
    "        p *= p_sensor(z[k], z_star_bel[k], robot.sensor)\n",
    "    end\n",
    "    \n",
    "    # Return the probability\n",
    "    return p\n",
    "end\n",
    "\n",
    "function sample_sensor(zk_star, model; dzk=0.01)\n",
    "    # Compute the CDF of the sensor probability distribution\n",
    "    sensor_cdf = cumsum([p_sensor(zk, zk_star, model) * dzk for zk in 0:dzk:model.zk_max+dzk])\n",
    "     \n",
    "    # Perform the inverse CDF trick through a search\n",
    "    idx = argmin(abs.(sensor_cdf .- rand()))\n",
    "\n",
    "    # Return the sampled sensor measurement\n",
    "    return dzk * (idx - 1)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function particle_filter(xs, u, z, robot, Δt, ϵ; use_update_step=true)\n",
    "    # Transform each particle using the kinematics model\n",
    "    xs = [robot.sample_kinematics(x, u, Δt, ϵ) for x in xs]\n",
    "        \n",
    "    # Perform the update step using the sensor measurements\n",
    "    if use_update_step\n",
    "        # Compute the particle importance factor\n",
    "        w = Weights([p_sensor_array(z, x, robot) for x in xs])\n",
    "        \n",
    "        # Perform the importance sampling step\n",
    "        xs = xs[sample(collect(1:length(xs)), w, length(xs))]\n",
    "    end\n",
    "    \n",
    "    # Return the new particles\n",
    "    return xs\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "struct robot_model\n",
    "    map\n",
    "    sample_kinematics\n",
    "    u_func\n",
    "    sensor\n",
    "    num_sensors\n",
    "    r\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function simulate(x0, robot, Δt, ϵ, num_iter, num_particles; use_update_step=true)\n",
    "    # Initialize a plot for the results\n",
    "    plot(aspect_ratio=:equal)\n",
    "    \n",
    "    # Initialize the ground truth state\n",
    "    x_gt = x0\n",
    "    \n",
    "    # Intitialize the dead reckoning state\n",
    "    x_dr = x0\n",
    "    \n",
    "    # Initialize the particles at the initial state\n",
    "    xs = [x0 for _ in 1:num_particles]\n",
    "    \n",
    "    # Plot the initial particle distribution\n",
    "    scatter!([Tuple(x[1:2]) for x in xs[1:num_particles]], markersize=1, markerstrokewidth=0, label=\"\")\n",
    "    \n",
    "    # Plot the map\n",
    "    plot_map(robot.map)\n",
    "    \n",
    "    # Plot the initial robot state\n",
    "    plot_robot(x_gt, robot.r, \"blue\")\n",
    "    \n",
    "    # Perform the simulation\n",
    "    for iter in 1:num_iter\n",
    "        # Get the current control from the control function\n",
    "        u = robot.u_func(Δt * iter)\n",
    "        \n",
    "        # Update the ground truth using the kinematics\n",
    "        x_gt_new = robot.sample_kinematics(x_gt, u, Δt, ϵ)\n",
    "        \n",
    "        # Update the dead reckoning using the kinematics with no error\n",
    "        x_dr_new = robot.sample_kinematics(x_dr, u, Δt, zeros(length(ϵ)))\n",
    "        \n",
    "        # Get the actual distances to objects along each sensor direction\n",
    "        z_star = get_distances(x_gt_new, robot)\n",
    "        \n",
    "        # Sample the actual sensor measurements\n",
    "        z = [sample_sensor(zk_star, robot.sensor) for zk_star in z_star]\n",
    "        \n",
    "        # Update the particles using the particle filter\n",
    "        xs_new = particle_filter(xs, u, z, robot, Δt, ϵ; use_update_step)\n",
    "        \n",
    "        # Plot the particles\n",
    "        scatter!([Tuple(x[1:2]) for x in xs_new[1:num_particles]], markersize=1, markerstrokewidth=0, \n",
    "                 label=iter == 1 ? \"Belief\" : \"\", color=:green)\n",
    "        \n",
    "        # Plot the ground truth robot state\n",
    "        plot_robot(x_gt_new, robot.r, \"blue\")\n",
    "        \n",
    "        # Plot the trajectory of the ground truth state from the previous iteration\n",
    "        plot!([x_gt[1], x_gt_new[1]], [x_gt[2], x_gt_new[2]], color=\"blue\", label=iter == 1 ? \"Ground Truth\" : \"\")\n",
    "        \n",
    "        # Plot the trajectory of the dead reckoning state from the previous iteration\n",
    "        plot!([x_dr[1], x_dr_new[1]], [x_dr[2], x_dr_new[2]], color=\"red\", label=iter == 1 ? \"Dead Reckoning\" : \"\")\n",
    "        \n",
    "        # Update the states of the simulation\n",
    "        xs, x_gt, x_dr = xs_new, x_gt_new, x_dr_new\n",
    "    end\n",
    "    \n",
    "    # Set plot parameters\n",
    "    plot!(grid=false, axis=nothing, border=:none, foreground_color_legend = nothing)\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Initial state of the robot\n",
    "x0 = [0, 0, π/3]\n",
    "\n",
    "# First control sequence, left turn\n",
    "u1 = [1, 0.5]\n",
    "\n",
    "# Second control sequence, right turn\n",
    "u2 = [1, -0.5]\n",
    "\n",
    "# Robot kinematic model disturbance parameters\n",
    "ϵ = [0.01, 0.01, 0.01, 0.01, 0.1, 0.1]\n",
    "\n",
    "# Robot radius\n",
    "r = 0.175\n",
    "\n",
    "# Simulation time-step\n",
    "Δt = 1\n",
    "\n",
    "# Number of sensors in the sensor array\n",
    "num_sensors = 16\n",
    "\n",
    "# Create environment and map\n",
    "box1 = create_box_obj([-4, 0], π/6)\n",
    "map = [box1, box2, box3]\n",
    "\n",
    "# Maximum sensor range\n",
    "zk_max = 5\n",
    "\n",
    "# Variance in sensor hit\n",
    "σ_hit = 0.5\n",
    "\n",
    "# Rate of early return for sensor\n",
    "λ_short = 1\n",
    "\n",
    "# Sensor noise mode weights\n",
    "weights = [0.7, 0.1, 0.1, 0.1]\n",
    "\n",
    "# Initialize the sensor\n",
    "sensor = sensor_model(zk_max, σ_hit, λ_short, weights)\n",
    "\n",
    "# Initialize the robot\n",
    "robot = robot_model(map, sample_robot_kinematics, t -> t ≤ 5 ? u1 : u2, sensor, num_sensors, r)\n",
    "\n",
    "# Set the number of particles to use\n",
    "num_particles = 1000"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "Random.seed!(0)\n",
    "sensor = sensor_model(5, 0.5, 1, [0.7, 0.1, 0.1, 0.1])\n",
    "zk_star = 3\n",
    "histogram([sample_sensor(zk_star, sensor) for _ in 1:10_000], normed=true, bins=50, label=\"\",\n",
    "          xticks=([0, zk_star, 5], [L\"0\", L\"z_{k}^{*}\", L\"z_{k}^{\\max}\"]),\n",
    "          xtickfont=14, xlims=(-0.5, 5.5), ylim=(0, 1.2), yaxis=false, grid=false,\n",
    "          lcolor=:blue, dpi=500)\n",
    "plot!(0:0.01:sensor.zk_max, zk -> p_sensor(zk, zk_star, sensor), label=\"\", linewidth=2)\n",
    "savefig(\"sensor-model.png\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Simulate the robot without the sensor updates\n",
    "Random.seed!(1)\n",
    "simulate(x0, robot, Δt, ϵ, 5, num_particles; use_update_step=false)\n",
    "plot!(xlims=(-6, 1), ylims=(-1, 4), legend=:topleft, dpi=500)\n",
    "savefig(\"robot-no-sensor.png\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Simulate the robot with the sensor updates\n",
    "Random.seed!(1)\n",
    "simulate(x0, robot, Δt, ϵ, 5, num_particles; use_update_step=true)\n",
    "plot!(xlims=(-6, 1), ylims=(-1, 4), legend=:topleft, dpi=500)\n",
    "savefig(\"robot-with-sensor.png\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Add more obstacles to the environment\n",
    "box2 = create_box_obj([-1, 5], π/3)\n",
    "box3 = create_box_obj([-7, 6], π/5)\n",
    "map = [box1, box2, box3]\n",
    "robot = robot_model(map, sample_robot_kinematics, t -> t ≤ 5 ? u1 : u2, sensor, num_sensors, r)\n",
    "\n",
    "# Simulate the robot in a larger environment with complex move\n",
    "Random.seed!(1)\n",
    "simulate(x0, robot, Δt, ϵ, 10, num_particles; use_update_step=true)\n",
    "plot!(xlims=(-10, 1), ylims=(-1, 8), legend=:left, dpi=500)\n",
    "savefig(\"robot-long.png\")"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.5.1",
   "language": "julia",
   "name": "julia-1.5"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.5.1"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
